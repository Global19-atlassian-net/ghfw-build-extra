# Maintainer: Brendan Forster <brendan@github.com>

pkgname=('github-extra')
pkgver=1.0.9
pkgrel=1
pkgdesc="Environment customizations for Git Shell"
arch=('any')
url="https://github.com/github/ghfw-build-extra"
license=('Apache 2.0')
install='github-extra.install'
depends=('git-extra')

prepare() {
  test $startdir/$pkgname.install -nt $startdir/$pkgname.install.in &&
  test $startdir/$pkgname.install -nt $startdir/resources/gitconfig &&
  test $startdir/$pkgname.install -nt $startdir/resources/gitattributes &&
  test $startdir/$pkgname.install -nt $startdir/resources/ssh_config ||
  sed -e "/^@@GITCONFIG@@$/r $startdir/resources/gitconfig" \
	-e "/^@@GITCONFIG@@$/d" \
	-e "/^@@GITATTRIBUTES@@$/r $startdir/resources/gitattributes" \
	-e "/^@@GITATTRIBUTES@@$/d" \
	-e "/^@@SSHCONFIG@@$/r $startdir/resources/ssh_config" \
	-e "/^@@SSHCONFIG@@$/d" \
	<$startdir/$pkgname.install.in >$startdir/$pkgname.install
}

package() {
  install -d -m755 $pkgdir/${MINGW_PREFIX}/etc
  install -m755 $srcdir/gitattributes.suggested $pkgdir/${MINGW_PREFIX}/etc/gitattributes.suggested
  install -m755 $srcdir/gitignore.suggested $pkgdir/${MINGW_PREFIX}/etc/gitignore.suggested
  install -d -m755 $pkgdir/usr/bin
  install -m755 $srcdir/ssh-noprompt $pkgdir/usr/bin/ssh-noprompt
}

